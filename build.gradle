buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.6'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}
dependencies {
    def libsdir = new File('libs');
    if (!libsdir.exists()) {
        println "Creating libs directory"
        libsdir.mkdir()
    }
    def prestoLib = new File('libs/presto_client-0.8.5.jar')
    if (!prestoLib.exists()) {
        println "Downloading presto library into libs folder"
        new URL('http://www.aocate.com/presto/client/presto_client-0.8.5.jar').withInputStream{ i -> prestoLib.withOutputStream{ it << i }}
    }

    compile 'com.android.support:appcompat-v7:18.0.+'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile ('org.shredzone.flattr4j:flattr4j-core:2.7') {
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.json', module: 'json'
    }
    compile 'commons-io:commons-io:2.4'
    compile 'com.nineoldandroids:library:2.4.0'
    compile project(':submodules:dslv:library')
    compile files('libs/presto_client-0.8.5.jar')
}

android {
    compileSdkVersion 18
    buildToolsVersion "17"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 18
        testPackageName "de.test.antennapod"
        testInstrumentationRunner "instrumentationTest.de.test.antennapod.AntennaPodTestRunner"
    }

    signingConfigs {
        releaseConfig {
            if (project.hasProperty('releaseStoreFile')) {
                storeFile file(releaseStoreFile)
            } else {
                storeFile file('keystore')
            }
            if (project.hasProperty('releaseStorePassword')) {
                storePassword releaseStorePassword
            } else {
                storePassword "password"
            }
            if (project.hasProperty('releaseKeyAlias')) {
                keyAlias releaseKeyAlias
            } else {
                keyAlias "alias"
            }
            if (project.hasProperty('releaseKeyPassword')) {
                keyPassword releaseKeyPassword
            } else {
                keyPassword "password"
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
        }
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.releaseConfig
        }
    }
}
