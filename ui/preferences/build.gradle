plugins {
    id("com.android.library")
}
apply from: "../../common.gradle"
apply from: "../../playFlavor.gradle"

android {
    namespace "de.danoeh.antennapod.ui.preferences"

    defaultConfig {
        def commit = ""
        try {
            def hashStdOut = new ByteArrayOutputStream()
            exec {
                commandLine "git", "rev-parse", "--short", "HEAD"
                standardOutput = hashStdOut
            }
            commit = hashStdOut.toString().trim()
        } catch (Exception ignore) {
        }
        buildConfigField "String", "COMMIT_HASH", ('"' + (commit.isEmpty() ? "Unknown commit" : commit) + '"')
    }
}

dependencies {
    implementation project(":event")
    implementation project(":net:common")
    implementation project(":net:sync:gpoddernet")
    implementation project(":storage:preferences")
    implementation project(":storage:importexport")
    implementation project(":ui:common")
    implementation project(":ui:i18n")
    implementation project(':net:sync:service-interface')
    implementation project(':net:sync:service')

    annotationProcessor "androidx.annotation:annotation:$annotationVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.fragment:fragment:$fragmentVersion"
    implementation "com.google.android.material:material:$googleMaterialVersion"
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation "androidx.work:work-runtime:$workManagerVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
    implementation "org.greenrobot:eventbus:$eventbusVersion"
    implementation 'com.github.ByteHamster:SearchPreference:v2.6.2'
}
