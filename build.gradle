buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.5'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint"
        }
    }
}

// Disable predex if requested (we can"t predex in Circle CI
// See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance
// and https://circleci.com/docs/android
project.ext.preDexLibs = !project.hasProperty("disablePreDex")

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

project.ext {
    compileSdkVersion = 28
    minSdkVersion = 16
    targetSdkVersion = 28

    workManagerVersion = "1.0.1"
    espressoVersion = "3.2.0"
    awaitilityVersion = "3.1.2"
    commonsioVersion = "2.5"
    commonslangVersion = "3.6"
    commonstextVersion = "1.3"
    eventbusVersion = "3.1.1"
    glideVersion = "4.8.0"
    glideOkhttpIntegrationVersion = "4.8.0"
    iconifyVersion = "2.2.2"
    jsoupVersion = "1.11.2"
    materialDialogsVersion = "0.9.0.2"
    okhttpVersion = "3.12.5"
    okioVersion = "1.17.4"
    recyclerviewFlexibledividerVersion = "1.4.0"
    robotiumSoloVersion = "5.6.3"
    rxAndroidVersion = "2.1.0"
    rxJavaVersion = "2.2.2"
    rxJavaRulesVersion = "1.3.3.0"
    triangleLabelViewVersion = "1.1.2"

    exoPlayerVersion = "2.10.4"
    audioPlayerVersion = "v1.0.17"

    castCompanionLibVer = "2.9.1"
    playServicesVersion = "8.4.0"
    wearableSupportVersion = "2.4.0"
}

wrapper {
    gradleVersion = "4.10.2"
}

// free build hack: common functions
def doFreeBuild() {
    return hasProperty("freeBuild")
}

apply plugin: "checkstyle"
checkstyle {
    toolVersion '8.24'
}

task checkstyle(type: Checkstyle) {
    classpath = files()
    source "${project.rootDir}"
    exclude("**/gen/**")
}
