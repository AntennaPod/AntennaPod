// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:2.3.3"
        classpath "me.tatarka:gradle-retrolambda:3.7.0"
        classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        // Exclude the version that the android plugin depends on.
        configurations.classpath.exclude group: "com.android.tools.external.lombok"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

// Disable predex if requested (we can"t predex in Circle CI
// See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance
// and https://circleci.com/docs/android
project.ext.preDexLibs = !project.hasProperty("disablePreDex")

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

project.ext {
    compileSdkVersion = 25
    buildToolsVersion = "25.0.3"
    minSdkVersion = 14
    targetSdkVersion = 25

    supportVersion = "25.3.1"
    commonsioVersion = "2.5"
    commonslangVersion = "3.6"
    eventbusVersion = "2.4.0"
    flattr4jVersion = "2.14"
    glideVersion = "3.8.0"
    glideOkhttpIntegrationVersion = "1.5.0"
    iconifyVersion = "2.2.2"
    jsoupVersion = "1.11.2"
    materialDialogsVersion = "0.9.0.2"
    okhttpVersion = "3.9.0"
    okioVersion = "1.13.0"
    recyclerviewFlexibledividerVersion = "1.2.6"
    robotiumSoloVersion = "5.6.3"
    rxAndroidVersion = "1.2.1"
    rxJavaVersion = "1.3.2"
    rxJavaRulesVersion = "1.3.2.0"
    triangleLabelViewVersion = "1.1.0"

    audioPlayerVersion = "v1.0.17"

    castCompanionLibVer = "2.9.1"
    playServicesVersion = "8.4.0"
    wearableSupportVersion = "2.0.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.4.1"
}

// free build hack: common functions
def doFreeBuild() {
    return hasProperty("freeBuild")
}
