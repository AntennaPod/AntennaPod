import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id("net.ltgt.errorprone") version "0.6"
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.3.2")
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        check("CatchAndPrintStackTrace", CheckSeverity.OFF)
        check("DefaultCharset", CheckSeverity.OFF)
        check("FutureReturnValueIgnored", CheckSeverity.OFF)
        check("ImmutableEnumChecker", CheckSeverity.OFF)
        check("InconsistentCapitalization", CheckSeverity.OFF)
        check("InputStreamSlowMultibyteRead", CheckSeverity.OFF)
        check("IntLongMath", CheckSeverity.OFF)
        check("IsInstanceOfClass", CheckSeverity.OFF)
        check("JdkObsolete", CheckSeverity.OFF)
        check("LockNotBeforeTry", CheckSeverity.OFF)
        check("MissingCasesInEnumSwitch", CheckSeverity.OFF)
        check("MissingOverride", CheckSeverity.OFF)
        check("NarrowingCompoundAssignment", CheckSeverity.OFF)
        check("ReferenceEquality", CheckSeverity.OFF)
        check("StringSplitter", CheckSeverity.OFF)
        check("ThreadPriorityCheck", CheckSeverity.OFF)
        check("TypeParameterUnusedInFormals", CheckSeverity.OFF)
        check("UnnecessaryParentheses", CheckSeverity.OFF)
    }
}

apply plugin: "com.android.library"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testApplicationId "de.danoeh.antennapod.core.tests"
        testInstrumentationRunner "de.danoeh.antennapod.core.AntennaPodTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "market"
    productFlavors {
        free {
            dimension "market"
        }
        play {
            dimension "market"
        }
    }

}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"
    implementation "com.android.support:percent:$supportVersion"
    implementation "org.apache.commons:commons-lang3:$commonslangVersion"
    implementation "org.apache.commons:commons-text:$commonstextVersion"
    implementation ("org.shredzone.flattr4j:flattr4j-core:$flattr4jVersion") {
        exclude group: "org.json", module: "json"
    }
    implementation "commons-io:commons-io:$commonsioVersion"
    implementation "com.jayway.android.robotium:robotium-solo:$robotiumSoloVersion"
    implementation "org.jsoup:jsoup:$jsoupVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideOkhttpIntegrationVersion@aar"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
    implementation "com.squareup.okio:okio:$okioVersion"
    implementation "de.greenrobot:eventbus:$eventbusVersion"
    implementation "io.reactivex:rxandroid:$rxAndroidVersion"
    implementation "org.awaitility:awaitility:$awaitilityVersion"

    implementation "com.google.android.exoplayer:exoplayer:$exoPlayerVersion"
    implementation "com.github.AntennaPod:AntennaPod-AudioPlayer:$audioPlayerVersion"

    // Add casting features
    // free build hack: skip some dependencies
    if (!doFreeBuild()) {
        playApi "com.google.android.libraries.cast.companionlibrary:ccl:$castCompanionLibVer"
        api "com.android.support:mediarouter-v7:$supportVersion"
        playApi "com.google.android.gms:play-services-cast:$playServicesVersion"
        api "com.google.android.support:wearable:$wearableSupportVersion"
    } else {
        System.out.println("core: free build hack, skipping some dependencies")
    }

    testImplementation 'junit:junit:4.12'

    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "skipped", "passed", "failed"
        showStandardStreams true
        displayGranularity 2
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint" << "-Xlint:-deprecation" << "-Xlint:-serial"
        }
    }
}
